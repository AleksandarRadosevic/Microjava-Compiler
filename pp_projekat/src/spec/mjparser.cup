




package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;

parser code {:

	Logger log = Logger.getLogger(getClass());
   	int lineDetected;
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol){
            msg.append (" na liniji ").append(((Symbol)info).left);
            lineDetected = ((Symbol)info).left;
        }
        log.error(msg.toString());
    }


:}

init with {:
	lineDetected = 0;
:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}


terminal Integer NUMBER, BOOLCONST;
terminal String IDENT;
terminal Character CHARCONST;

terminal PROG, LBRACECURLY, RBRACECURLY, CONST, ASSIGN, EQUAL, NOTEQUAL, GREATER, GREATEREQUAL, THIS, SUPER, ENUM;
terminal COMMA, CLASS, EXTENDS, RECORD, LPAREN, RPAREN, VOID, PRINT, GOTO, AND, NEW, DOT, PLUS, MINUS, MUL, DIV, MOD, COLON;
terminal IF, SEMICOLON, DO, WHILE, BREAK, CONTINUE, RETURN, READ, LBRACE, RBRACE, INCREMENT, DECREMENT,LOWER, LOWEREQUAL, OR, ELSE;

nonterminal DeclarationList, ConstDecl, VarDecl, ClassDecl, RecordDecl, ConstVal, ConstDeclList;
nonterminal OptionalBraces, VarDeclList, OptionalExtends, DeclListVar, DeclList;
nonterminal OptConstructorMethod, ConstructorDecl, OptionalConstructorDecl, DeclListMethod, StatementList;
nonterminal FormParsOptional, Statement, FormPars, Pars, ParsList, SingleStatement, Label, LabelColon;
nonterminal Condition, OptionalElse, DesignatorStatement, DesignatorStatementElements, ExprCommaList;
nonterminal Expr, OptionalNumConst, Statements, OptionalExpression, ActParsOptional, ActPars, CondTerm;
nonterminal CondTermListOr, CondFactListAnd, CondFact, RelopOprOptional, Relop, OptionalMinus, Term, AddopTermList;
nonterminal Addop, Factor, MulopFactorList, Mulop, ActParsBracesOptional, ExprBracesOptional, IdentList, DotOrExpr, Assignop;
nonterminal ConditionIf;


nonterminal rs.etf.pp1.symboltable.concepts.Obj ProgName, Program, MethodTypeName, MethodDecl, Designator;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Type, TypeOrVoid;

precedence left ELSE;


Program ::= (Program) PROG ProgName:p DeclarationList LBRACECURLY DeclListMethod RBRACECURLY;

ProgName ::= (ProgName) IDENT:progName;

DeclarationList ::= (DeclarationListc) DeclarationList DeclList
				| (NoDeclarations) /* epsilon */;
				
DeclList ::= (DeclListc)  
				ConstDecl  |
				VarDecl    | 
				ClassDecl  | 
				RecordDecl;
			

ConstDecl ::= (ConstDeclc) CONST Type:constType IDENT ASSIGN ConstVal ConstDeclList SEMICOLON
			| (ErrorConstDeclEnd) error SEMICOLON:l
		    {: parser.report_error("Izvrsen oporavak do ; u liniji " + lleft, null);  :}
		    ;


ConstVal ::= (ConstValc) NUMBER | CHARCONST | BOOLCONST;

ConstDeclList ::= (ConstDeclListc) ConstDeclList COMMA IDENT ASSIGN ConstVal  
				| (ErrorComma) error
		    	{: parser.report_error("Izvrsen oporavak do , u okviru definisanje globalnih promenljivih ", null);  :}
				| (NoConstDecl) /* epsilon */;
	

VarDecl ::= (VarDeclc) Type:varType IDENT:varName OptionalBraces VarDeclList SEMICOLON;

OptionalBraces ::= (OptionalBracesc) LBRACE RBRACE 
				| (NoOptionalBraces) /* epsilon */;

VarDeclList ::= (VarDeclListc) VarDeclList COMMA IDENT OptionalBraces  
				| (NoVarDeclList) /* epsilon */;
	
			
ClassDecl ::= (ClassDeclc) CLASS IDENT OptionalExtends LBRACECURLY DeclListVar OptConstructorMethod RBRACECURLY;			 

OptionalExtends ::= (OptionalExtendsc) EXTENDS Type
				| (NoOptionalExtendsc) /* epsilon */;
 
DeclListVar ::=	(DeclListVarc) DeclListVar VarDecl 
				| (NoDeclListVar) /* epsilon */;	

/* treba da bude constructordecl optional */
OptConstructorMethod ::= (OptConstructorMethodc) LBRACECURLY ConstructorDecl DeclListMethod RBRACECURLY
				| (NoOptConstructorMethod) /* epsilon */;	
				
OptionalConstructorDecl ::= (OptionalConstructorDeclc) ConstructorDecl
				| (NoOptionalConstructorDecl) /* epsilon */;	
				
DeclListMethod ::= (DeclListMethodc) DeclListMethod MethodDecl 
				| (NoDeclListMethod) /* epsilon */;	


RecordDecl ::= (RecordDeclc) RECORD IDENT LBRACECURLY DeclListVar RBRACECURLY;

ConstructorDecl ::= (ConstructorDeclc) IDENT LPAREN RPAREN DeclListVar LBRACECURLY StatementList RBRACECURLY;


MethodDecl ::= (MethodDeclc) MethodTypeName LPAREN FormParsOptional  DeclListVar LBRACECURLY StatementList RBRACECURLY;

MethodTypeName ::= (MethodTypeNamec) TypeOrVoid:retType IDENT:methodName;


TypeOrVoid ::= (TypeOrVoidc) Type | VOID;

FormParsOptional ::= (FormParsOptionalc) FormPars
				| RPAREN;
						
						
FormPars ::= (FormParsc) Pars ParsList RPAREN
			| (ErrorFormPars) error RPAREN:l
		   	{: parser.report_error("Izvrsen oporavak do ) u liniji "+ lleft, null);  :};
		
Pars ::= (Parsc) Type IDENT OptionalBraces;

ParsList ::= (ParsListc) ParsList COMMA Pars 
			| (NoParsList) /* epsilon */;

Type ::= (Typec) IDENT:typeName;


Statement ::= (Statementc) LabelColon SingleStatement 
			| (ErrorStmt) error SEMICOLON:l
		   	{: parser.report_error("Izvrsen oporavak do ; u liniji " + lleft, null);  :}
			| SingleStatement
			| Statements;
			


LabelColon ::= (LabelColonc) Label COLON;

SingleStatement ::= (SingleStatementc) DesignatorStatement SEMICOLON
				| (IfStmt) IF LPAREN ConditionIf Statement ELSE Statement 
				| (NoElseStmt) IF LPAREN ConditionIf Statement
			 	| (DoStmt) DO Statement WHILE LPAREN Condition RPAREN SEMICOLON
				| (BreakStmt) BREAK SEMICOLON
				| (ContinueStmt) CONTINUE SEMICOLON
				| (ReturnStmt) RETURN OptionalExpression SEMICOLON
				| (ReadStmt) READ LPAREN Designator RPAREN SEMICOLON
				| (PrintStmt) PRINT LPAREN Expr OptionalNumConst RPAREN SEMICOLON
				| (GotoStmt) GOTO Label SEMICOLON;
				
OptionalElse ::= (OptionalElsec) ELSE Statement
				| (NoOptionalElse) /* epsilon */;

OptionalExpression ::= (OptionalExpressionc) Expr 
					| (NoOptionalExpression) /* epsilon */;

OptionalNumConst ::= (OptionalNumConstc) COMMA NUMBER
				| (NoOptionalNumConst) /* epsilon */;
				
				
Statements ::= (Statementsc) LBRACECURLY StatementList RBRACECURLY;
StatementList ::= (StatementListc) StatementList Statement
				| (NoStatementList) /* epsilon */;				
				
DesignatorStatement ::= (DesignatorStatementc) Designator DesignatorStatementElements;				
				
DesignatorStatementElements ::= (DesignatorStatementElementsc) Assignop Expr
					| LPAREN ActParsOptional RPAREN 
					| INCREMENT
					| DECREMENT;				


ActParsOptional ::= (ActParsOptionalc) ActPars 
				| (NoActPars) /* epsilon */;


ActPars ::= (ActParsc) Expr ExprCommaList;

ExprCommaList ::= (ExprCommaListc) ExprCommaList COMMA Expr 
				| (NoExprCommaList) /* epsilon */;

ConditionIf ::= (ConditionIfc) CondTerm CondTermListOr RPAREN
			| (ErrorConditionIf) error RPAREN
		   	{: parser.report_error("Izvrsen oporavak do ) u liniji " + parser.lineDetected, null);  :};

Condition ::= (Conditionc) CondTerm CondTermListOr;
CondTermListOr ::= (CondTermListOrc) CondTermListOr OR CondTerm
				| (NoCondTermListOr) /* epsilon */;

CondTerm ::= (CondTermc) CondFact CondFactListAnd;
CondFactListAnd ::= (CondFactListAndc) CondFactListAnd AND CondFact
				| (NoCondFactListAnd) /* epsilon */;
				
CondFact ::= (CondFactc) Expr RelopOprOptional;				 
				
RelopOprOptional ::= (RelopOprOptionalc) Relop Expr
				| (NoRelopOprOptional) /* epsilon */;				
				
				
Expr ::= (Exprc) OptionalMinus Term AddopTermList;

OptionalMinus ::= (OptionalMinus) MINUS 
			 | (NoOptionalMinus) /* epsilon */;

AddopTermList ::= (AddopTermListc) AddopTermList Addop Term
				| (NoAddopTermList) /* epsilon */;

Term ::= (Termc) Factor MulopFactorList;

MulopFactorList ::= (MulopFactorListc) MulopFactorList Mulop Factor
				| (NoMulopFactorList) /* epsilon */;

Factor ::= (Factorc) Designator ActParsBracesOptional
		| NUMBER
		| CHARCONST
		| BOOLCONST
		| NEW Type ExprBracesOptional
		| LPAREN Expr RPAREN;
		
ActParsBracesOptional ::= (ActParsBracesOptionalc) LPAREN ActParsOptional RPAREN 		
						| (NoActParsBracesOptional) /* epsilon */;
		
ExprBracesOptional ::= (ExprBracesOptionalc) LBRACE Expr RBRACE
					| (NoExprBracesOptional) /* epsilon */;
					
					
Designator ::= (Designatorc) IDENT:name 
			| IDENT:name LBRACE Expr RBRACE
			| IdentList;

IdentList ::= (IdentListc) IDENT DOT IDENT
		 	| IDENT DOT IDENT LBRACE Expr RBRACE;
			
DotOrExpr ::= (DotOrExprc) DOT IDENT 
			| LBRACE Expr RBRACE;
			
Label ::= (Labelc) IDENT;

Assignop ::= (Assignopc) ASSIGN;

Relop ::= (Relopc) EQUAL
		| NOTEQUAL
		| GREATER
		| GREATEREQUAL
		| LOWER
		| LOWEREQUAL;
		
Addop ::= (AddOpc) PLUS | MINUS;

Mulop ::= (Mulopc) MUL | DIV | MOD;

