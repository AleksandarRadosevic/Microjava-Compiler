




package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;

parser code {:

	Logger log = Logger.getLogger(getClass());
   	int lineDetected;
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol){
            msg.append (" na liniji ").append(((Symbol)info).left);
            lineDetected = ((Symbol)info).left;
        }
        log.error(msg.toString());
    }


:}

init with {:
	lineDetected = 0;
:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}


terminal Integer NUMBER;
terminal Boolean BOOLCONST;
terminal String IDENT;
terminal Character CHARCONST;

terminal PROG, LBRACECURLY, RBRACECURLY, CONST, ASSIGN, EQUAL, NOTEQUAL, GREATER, GREATEREQUAL, THIS, SUPER, ENUM;
terminal COMMA, CLASS, EXTENDS, RECORD, LPAREN, RPAREN, VOID, PRINT, GOTO, AND, NEW, DOT, PLUS, MINUS, MUL, DIV, MOD, COLON;
terminal IF, SEMICOLON, DO, WHILE, BREAK, CONTINUE, RETURN, READ, LBRACE, RBRACE, INCREMENT, DECREMENT,LOWER, LOWEREQUAL, OR, ELSE;
terminal DEGREE, THREEDOTS;

nonterminal DeclarationList, ConstDecl, VarDecl, ClassDecl, ConstVal, ConstDeclList;
nonterminal OptionalBraces, VarDeclList, OptionalExtends, DeclListVar, DeclList;
nonterminal OptConstructorMethod, ConstructorDecl, OptionalConstructorDecl, DeclListMethod, StatementList;
nonterminal FormParsOptional, Statement, FormPars, Pars, ParsList, SingleStatement, Label, LabelColon;
nonterminal Condition, OptionalElse, DesignatorStatement, DesignatorStatementElements, ExprCommaList;
nonterminal OptionalNumConst, Statements, OptionalExpression, ActParsOptional, ActPars, CondTerm;
nonterminal CondTermListOr, CondFactListAnd, CondFact, Relop, OptionalMinus;
nonterminal Addop, Mulop, Assignop;
nonterminal ConditionIf, VarDeclGlobal, VarDeclListGlobal, Expop, ListBaseExp, VarArgs;
nonterminal DesignatorCalled;

nonterminal rs.etf.pp1.symboltable.concepts.Obj ProgName, Program, MethodTypeName, MethodDecl, Designator, DesignatorName, DesignatorNameArr;
nonterminal rs.etf.pp1.symboltable.concepts.Obj RecordDecl, RecordName;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Type, TypeOrVoid, Term, Factor, Expr, ExprPar, BaseExp, AddopTermList, MulopFactorList, TermMinus;

precedence left ELSE;


Program ::= (Programc) PROG ProgName:p DeclarationList LBRACECURLY DeclListMethod RBRACECURLY;

ProgName ::= (ProgNamec) IDENT:progName;

DeclarationList ::= (DeclarationListc) DeclarationList DeclList
				| (NoDeclarations) /* epsilon */;
				
DeclList ::= (DeclListc)  ConstDecl
			| VarDeclGlobal
			| ClassDecl
			| RecordDecl;

ConstDecl ::= (ConstDeclc) CONST Type:constType IDENT:name ASSIGN ConstVal ConstDeclList SEMICOLON
			| (ErrorConstDeclEnd) error SEMICOLON:l
		    {: parser.report_error("Izvrsen oporavak do ; u liniji " + lleft, null);  :}
		    ;


ConstVal ::= (ConstInt) NUMBER |
			(ConstChar) CHARCONST |
			(ConstBool) BOOLCONST;

ConstDeclList ::= (ConstDeclListc) ConstDeclList COMMA IDENT ASSIGN ConstVal  
				| (ErrorComma) error
		    	{: parser.report_error("Izvrsen oporavak do , u okviru definisanje globalnih promenljivih ", null);  :}
				| (NoConstDecl) /* epsilon */;
	

VarDeclGlobal ::= (VarDeclGlobalc) Type:varType IDENT:varName OptionalBraces VarDeclListGlobal SEMICOLON;

VarDecl ::= (VarDeclc) Type:varType IDENT:varName OptionalBraces VarDeclList SEMICOLON;

OptionalBraces ::= (OptionalBracesc) LBRACE RBRACE 
				| (NoOptionalBraces) /* epsilon */;

VarDeclList ::= (VarDeclListc) VarDeclList COMMA IDENT OptionalBraces  
				| (NoVarDeclList) /* epsilon */;
			
				
VarDeclListGlobal ::= (VarDeclListGlobalc) VarDeclListGlobal COMMA IDENT OptionalBraces  
				| (NoVarDeclListGlobalc) /* epsilon */;
								
				
ClassDecl ::= (ClassDeclc) CLASS IDENT OptionalExtends LBRACECURLY DeclListVar OptConstructorMethod RBRACECURLY;			 

OptionalExtends ::= (OptionalExtendsc) EXTENDS Type
				| (NoOptionalExtendsc) /* epsilon */;
 
DeclListVar ::=	(DeclListVarc) DeclListVar VarDecl 
				| (NoDeclListVar) /* epsilon */;	

/* treba da bude constructordecl optional */
OptConstructorMethod ::= (OptConstructorMethodc) LBRACECURLY DeclListMethod RBRACECURLY
				| (NoOptConstructorMethod) /* epsilon */;	
				
OptionalConstructorDecl ::= (OptionalConstructorDeclc) ConstructorDecl
				| (NoOptionalConstructorDecl) /* epsilon */;	
				
DeclListMethod ::= (DeclListMethodc) DeclListMethod MethodDecl 
				| (NoDeclListMethod) /* epsilon */;	


RecordDecl ::= (RecordDeclc) RECORD RecordName LBRACECURLY DeclListVar RBRACECURLY;

RecordName ::= (RecordName) IDENT:name;

ConstructorDecl ::= (ConstructorDeclc) IDENT LPAREN RPAREN DeclListVar LBRACECURLY StatementList RBRACECURLY;


MethodDecl ::= (MethodDeclc) MethodTypeName LPAREN FormParsOptional:pars DeclListVar LBRACECURLY StatementList RBRACECURLY;

MethodTypeName ::= (MethodTypeNamec) Type:varType IDENT:methodName
				| (VoidFunc) VOID IDENT:methodName;


TypeOrVoid ::= (TypeOrVoidc) Type:returnType | VOID;

FormParsOptional ::= (FormParsOptionalc) FormPars
				| FormPars COMMA VarArgs
				| VarArgs
				| RPAREN;
						
						
FormPars ::= (FormParsc) Pars ParsList RPAREN
			| (ErrorFormPars) error RPAREN:l
		   	{: parser.report_error("Izvrsen oporavak do ) u liniji "+ lleft, null);  :};
		
Pars ::= (Parsc) Type:typeName IDENT:name
	  | Type:typeName IDENT:name LBRACE RBRACE;

ParsList ::= (ParsListc) ParsList COMMA Pars 
			| (NoParsList) /* epsilon */;

Type ::= (Typec) IDENT:typeName;


Statement ::= (Statementc) LabelColon SingleStatement 
			| (ErrorStmt) error SEMICOLON:l
		   	{: parser.report_error("Izvrsen oporavak do ; u liniji " + lleft, null);  :}
			| SingleStatement
			| Statements;
			


LabelColon ::= (LabelColonc) Label COLON;

SingleStatement ::= (SingleStatementc) DesignatorStatement SEMICOLON
				| (IfStmt) IF LPAREN ConditionIf Statement ELSE Statement 
				| (NoElseStmt) IF LPAREN ConditionIf Statement
			 	| (DoWhileStmt) DO Statement WHILE LPAREN Condition RPAREN SEMICOLON
				| (BreakStmt) BREAK SEMICOLON
				| (ContinueStmt) CONTINUE SEMICOLON
				| (ReturnStmt) RETURN OptionalExpression SEMICOLON
				| (ReadStmt) READ LPAREN Designator RPAREN SEMICOLON
				| (PrintStmt) PRINT LPAREN Expr OptionalNumConst RPAREN SEMICOLON
				| (GotoStmt) GOTO Label SEMICOLON;
				
OptionalElse ::= (OptionalElsec) ELSE Statement
				| (NoOptionalElse) /* epsilon */;

OptionalExpression ::= (OptionalExpressionc) Expr 
					| (NoOptionalExpression) /* epsilon */;

OptionalNumConst ::= (OptionalNumConstc) COMMA NUMBER
				| (NoOptionalNumConst) /* epsilon */;
				
				
Statements ::= (Statementsc) LBRACECURLY StatementList RBRACECURLY;
StatementList ::= (StatementListc) StatementList Statement
				| (NoStatementList) /* epsilon */;				
				
DesignatorStatement ::= (DesignatorAssign) Designator Assignop Expr
					| (DesignatorMethod) DesignatorCalled LPAREN ActParsOptional RPAREN
					| (DesignatorIncrement) Designator INCREMENT
					| (DesignatorDecrement) Designator DECREMENT;				
				
DesignatorStatementElements ::= (DesignatorStatementElementsc) Assignop Expr
					| LPAREN ActParsOptional RPAREN 
					| INCREMENT
					| DECREMENT;				


ActParsOptional ::= (ActParsOptionalc) ActPars 
				| (NoActPars) /* epsilon */;


ActPars ::= (ActParsc) ExprPar ExprCommaList;

ExprPar ::= (ExprPar) Expr;

ExprCommaList ::= (ExprCommaListc) ExprCommaList COMMA ExprPar 
				| (NoExprCommaList) /* epsilon */;

ConditionIf ::= (ConditionIfc) CondTerm CondTermListOr RPAREN
			| (ErrorConditionIf) error RPAREN
		   	{: parser.report_error("Izvrsen oporavak do ) u liniji " + parser.lineDetected, null);  :};

Condition ::= (Conditionc) CondTerm CondTermListOr;
CondTermListOr ::= (CondTermListOrc) CondTermListOr OR CondTerm
				| (NoCondTermListOr) /* epsilon */;

CondTerm ::= (CondTermc) CondFact CondFactListAnd;
CondFactListAnd ::= (CondFactListAndc) CondFactListAnd AND CondFact
				| (NoCondFactListAnd) /* epsilon */;
				
CondFact ::= (CondFactc) Expr 
			| (ExprRelop) Expr:firstExpr Relop Expr:secondExpr;				 
									
Expr ::= (Exprc) TermMinus AddopTermList;

TermMinus ::= (TermMinusc) OptionalMinus Term;


OptionalMinus ::= (OptionalMinusc) MINUS 
			 | (NoOptionalMinus) /* epsilon */;

AddopTermList ::= (AddopTermListc) AddopTermList Addop Term
				| (NoAddopTermList) /* epsilon */;

Term ::= (Termc) Factor MulopFactorList;

MulopFactorList ::= (MulopFactorListc) MulopFactorList Mulop Factor
				| (NoMulopFactorList) /* epsilon */;


Factor ::= (Factorc) BaseExp ListBaseExp;


Expop ::= (Expopc) DEGREE;

ListBaseExp ::= (ListBaseExpc) ListBaseExp Expop BaseExp
			| (NoListBaseExpc) /* epsilon */;


BaseExp ::= (BaseNumber) NUMBER
		| (BaseChar) CHARCONST
		| (BaseBool) BOOLCONST
		| (BaseNewRecord) NEW Type:name
		| (BaseNewArrayElem) NEW Type LBRACE Expr RBRACE
		| (BaseExpr) LPAREN Expr RPAREN
		| (BaseDesignator) Designator
		| (FuncCall) DesignatorCalled LPAREN ActParsOptional RPAREN;
				
DesignatorCalled ::= (DesignatorCalled) Designator;
							
Designator ::= (Designatorc) DesignatorName 
			| (ArrayElem) DesignatorNameArr LBRACE Expr RBRACE
			| DesignatorName DOT IDENT
			| (ArrayElemClass) DesignatorName DOT IDENT LBRACE Expr RBRACE;

DesignatorName ::= (DesignatorName) IDENT:name;
DesignatorNameArr ::= (DesignatorNameArr) IDENT:name;

Label ::= (Labelc) IDENT;

Assignop ::= (Assignopc) ASSIGN;

Relop ::= (Relopc) EQUAL
		| NOTEQUAL
		| GREATER
		| GREATEREQUAL
		| LOWER
		| LOWEREQUAL;
		
Addop ::= (OperationPlus) PLUS | (OperationMinus) MINUS;

Mulop ::= (OperationMul) MUL | (OperationDiv) DIV | (OperationMod) MOD;


VarArgs ::= (VarArgsc) Type THREEDOTS IDENT;

